plugins {
    id 'java'
    id 'pmd'
    id 'jacoco'
}

allprojects {
    apply plugin: 'pmd'
    apply plugin: 'java'
    apply plugin: 'jacoco'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacoco {
        toolVersion = "0.8.11"
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.required = true
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.6
                }
            }
        }
    }

    pmd {
        toolVersion = '6.50.0'
        ruleSetFiles = files("$rootDir/config/pmd/ruleset.xml")
    }

    tasks.withType(Pmd).configureEach {
        reports {
            xml.required.set(true)
            html.required.set(true)
        }
    }

    check.dependsOn pmdMain
    check.dependsOn pmdTest
    check.dependsOn jacocoTestReport
    check.dependsOn jacocoTestCoverageVerification
}
